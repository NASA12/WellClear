horizontal_RA[TAU:nnreal,DMOD:posreal] : THEORY
BEGIN

  IMPORTING vectors@vectors_2D,
            tcas_tau[DMOD]

  so,vo,
  si,vi : VAR Vect2  
  t     : VAR nnreal

  horizontal_RA(so,vo,si,vi): bool =
    LET s=so-si,v=vo-vi IN
      IF s*v >= 0 THEN norm(s)<=DMOD
      ELSE norm(s)<=DMOD OR tau_current(so,vo,si,vi) <= TAU
      ENDIF

  horizontal_RA_at(so,vo,si,vi)(t): bool =
    horizontal_RA(so+t*vo,vo,si+t*vi,vi)

  horizontal_RA_at_rewrite: LEMMA LET s=so-si,v=vo-vi IN
    horizontal_RA_at(so,vo,si,vi)(t) IFF 
    (sqv(s+t*v)<=sq(DMOD) OR (sqv(s+t*v)>sq(DMOD) AND s*v<0 AND (s+t*v)*v<=0 AND
     sqv(s+t*v) + TAU*(s+t*v)*v - sq(DMOD) <= 0))

  horizontal_ra?(so,vo,si,vi): bool = 
    EXISTS (t): horizontal_RA_at(so,vo,si,vi)(t)

  ra_tau_at(so,vo,si,vi)(t): bool = % does not include cd2d[DMOD] when diverging
    LET s=so-si,v=vo-vi IN
    s*v<0 AND (s+t*v)*v<=0 AND
    sqv(s+t*v) + TAU*(s+t*v)*v - sq(DMOD) <= 0

END horizontal_RA
