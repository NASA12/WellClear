WCV_taumod[TAUMOD,TCOA:nnreal,DTHR,ZTHR:posreal]  : THEORY 

% TAUMOD is the modified time threshold
% TCOA is the time to co-altitude threshold
% DTHR is the distance threshold
% ZTHR is the altitude threshold

BEGIN

  IMPORTING horizontal_WCV_taumod[TAUMOD,DTHR],
  	    WCV[TAUMOD,TCOA,DTHR,ZTHR],
	    vectors@vectors_3D,
  	    ACCoRD@cd3d

  s,v : VAR Vect3  
  B   : VAR nnreal
  T   : VAR posreal

  WCV_taumod(s,v): MACRO bool =
     WCV(taumod)(s,v)

  WCV_taumod_symmetric : THEOREM
    WCV_symmetric?(taumod)

  WCV_taumod_local_convexity: THEOREM
    WCV_locally_convex?(taumod)

  WCV_taumod_interval(B,(T|B<T),s,v): MACRO EntryExit[B,T] = 
    WCV_interval(taumod,horizontal_WCV_taumod_interval)(B,T,s,v)

  WCV_taumod_interval_correct: LEMMA B<T IMPLIES
    FORALL (t:Lookahead[B,T]):
      LET interval = WCV_taumod_interval(B,T,s,v),
          tentry = interval`entry,
	  texit  = interval`exit IN
      WCV_taumod(s+t*v,v) IFF (tentry<=t AND t<=texit)

  WCV_taumod_detection(B,T,s,v): MACRO bool =
    WCV_detection(taumod,horizontal_WCV_taumod_interval)(B,T,s,v)

  WCV_taumod_detection_correct: LEMMA B<T IMPLIES
    (WCV_taumod_detection(B,T,s,v) IFF
    (EXISTS (t:Lookahead[B,T]): WCV_taumod(s+t*v,v)))

END WCV_taumod
