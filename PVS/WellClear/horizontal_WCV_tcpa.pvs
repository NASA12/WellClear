horizontal_WCV_tcpa[TCPA:nnreal,DTHR:posreal]  : THEORY 

% TCPA is the time to closest point of approach threshold
% DTHR is the distance thershold

BEGIN

  IMPORTING horizontal_WCV_tcpa0[TCPA,DTHR,DTHR],
            horizontal_WCV[TCPA,DTHR]

  s,v : VAR Vect2  
  T   : VAR posreal

  % You are not well clear if either inside DTHR or the dist at tca is at most DTHR and
  % the time to CPA is at most TCPA

  tcpa_timevar : JUDGEMENT 
    tcpa HAS_TYPE (pre_timevar?)

  horizontal_WCV_tcpa(s,v): MACRO bool =
    horizontal_WCV(tcpa)(s,v)

  horizontal_WCV_tcpa_symm : LEMMA
    horizontal_WCV_tcpa(s,v) = horizontal_WCV_tcpa(-s,-v)    

  horizontal_WCV_tcpa_interval(T,s,v): EntryExit[0,T] =
    IF    sqv(v) = 0 AND sqv(s)<=sq(DTHR) THEN WholeInterval[0,T]
    ELSIF sqv(v)=0 THEN EmptyInterval[0,T] % detection returns false
    ELSIF sqv(s)<=sq(DTHR) THEN (# entry:=0,exit:=min(T,Theta_D[DTHR](s,v,1)) #)
    ELSIF s*v>0 THEN EmptyInterval[0,T] % detection returns false
    ELSIF sqv(s + horizontal_tca(s,v)*v)>sq(DTHR) THEN (# entry:=T,exit:=0 #) 
    ELSIF Delta[DTHR](s,v)<0 AND horizontal_tca(s,v)-TCPA>T 
      THEN EmptyInterval[0,T] % detection returns false
    ELSIF Delta[DTHR](s,v)<0 THEN 
         (# entry:=max(0,horizontal_tca(s,v)-TCPA), 
    	     exit:=min(T,horizontal_tca(s,v)) #)
    ELSE LET tmin = min(Theta_D[DTHR](s,v,-1),horizontal_tca(s,v)-TCPA) IN
      IF tmin > T THEN EmptyInterval[0,T] % detection returns false
      ELSE  (# entry:= max(0,tmin), exit:=min(T,Theta_D[DTHR](s,v,1)) #)
      ENDIF
    ENDIF

  horizontal_WCV_tcpa_eq : LEMMA	
    horizontal_WCV_tcpa(s,v) = horizontal_WCV_tcpa0(s,v)

  horizontal_WCV_tcpa_rew : LEMMA
    horizontal_WCV_tcpa(s,v) IFF
      (sqv(s) <= sq(DTHR) OR
       (s*v < 0 AND 
        sqv(s+horizontal_tca(s,v)*v)<=sq(DTHR) AND 
        horizontal_tca(s,v) <=TCPA))

  horizontal_WCV_tcpa_interval_eq : LEMMA
    horizontal_WCV_tcpa_interval(T,s,v) = 
    horizontal_WCV_tcpa0_interval(T,s,v)

  horizontal_WCV_tcpa_interval_def: LEMMA FORALL (t:Lookahead[0,T]):
    LET nwcentryexit = horizontal_WCV_tcpa_interval(T,s,v),
        (nwcentry,nwcexit) = (nwcentryexit`entry,nwcentryexit`exit)
    IN
      horizontal_WCV_tcpa(s+t*v,v) IFF
      (nwcentry<=t AND t<=nwcexit)

  horizontal_WCV_tcpa_interval_correct : LEMMA
    horizontal_interval_correct?(tcpa)(horizontal_WCV_tcpa_interval)

  horizontal_WCV_tcpa_interval_sound : LEMMA
    horizontal_interval_sound?(tcpa)(horizontal_WCV_tcpa_interval)

END horizontal_WCV_tcpa
