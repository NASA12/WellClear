WCV_tep[TEP,TCOA:nnreal,DTHR,ZTHR:posreal]  : THEORY 

% TEP is the time to entry point threshold
% TCOA is the time to co-altitude threshold
% DTHR is the distance threshold
% ZTHR is the altitude threshold

BEGIN

  IMPORTING horizontal_WCV_tep[TEP,DTHR],
  	    WCV[TEP,TCOA,DTHR,ZTHR],
	    vectors@vectors_3D,
  	    ACCoRD@cd3d

  s,v : VAR Vect3  
  B   : VAR nnreal
  T   : VAR posreal

  WCV_tep(s,v): MACRO bool =
     WCV(tep)(s,v)

  WCV_tep_symmetric : THEOREM
    WCV_symmetric?(tep)

  WCV_tep_local_convexity: THEOREM
    WCV_locally_convex?(tep)

  WCV_tep_interval(B,(T|B<T),s,v): MACRO EntryExit[B,T] = 
    WCV_interval(tep,horizontal_WCV_tep_interval)(B,T,s,v)

  WCV_tep_interval_correct: LEMMA B<T IMPLIES
    FORALL (t:Lookahead[B,T]):
      LET interval = WCV_tep_interval(B,T,s,v),
          tentry = interval`entry,
	  texit  = interval`exit IN
      WCV_tep(s+t*v,v) IFF (tentry<=t AND t<=texit)

  WCV_tep_detection(B,T,s,v): MACRO bool =
    WCV_detection(tep,horizontal_WCV_tep_interval)(B,T,s,v)

  WCV_tep_detection_correct: LEMMA B<T IMPLIES
    (WCV_tep_detection(B,T,s,v) IFF
    (EXISTS (t:Lookahead[B,T]): WCV_tep(s+t*v,v)))

END WCV_tep
